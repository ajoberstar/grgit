buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/groups/public/' }
	}
	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.6.5'
		classpath 'org.ajoberstar:gradle-imports:0.1.0'
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
	}
}

group = 'org.ajoberstar'
description = 'The Groovy way to use Git.'
version = '0.2.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'github-pages'
apply plugin: 'sonar-runner'
apply plugin: 'organize-imports'
apply from: rootProject.file('gradle/publishing.gradle')
apply from: rootProject.file('gradle/license.gradle')

sonarRunner.sonarProperties {
	property 'sonar.language', 'grvy'
	property 'sonar.projectVersion', '0.2.0'
}

sourceCompatibility = '1.7'

jacoco.toolVersion = '0.7.0.201403182114'

repositories {
	mavenCentral()
}

def jgitVersion = '3.3.0.201403021825-r'

dependencies {
	// groovy
	compile localGroovy()

	// jgit
	compile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
	compile "org.eclipse.jgit:org.eclipse.jgit.ui:$jgitVersion"

	// agentproxy
	['jsch', 'pageant', 'sshagent', 'usocket-jna'].each {
		compile "com.jcraft:jsch.agentproxy.${it}:0.0.7"
	}

	// logging
	compile 'org.slf4j:slf4j-api:1.7.6'
	testRuntime 'org.slf4j:slf4j-simple:1.7.6'

	// testing
	testCompile 'junit:junit:4.11'
	testCompile('org.spockframework:spock-core:0.7-groovy-1.8') { exclude group: 'org.codehaus.groovy' }
}

tasks.withType(Test) {
	reports {
		junitXml.outputPerTestCase = true
	}
}

test {
	useJUnit {
		excludeCategories 'org.ajoberstar.grgit.fixtures.PlatformSpecific'
	}
}

['Windows', 'Linux', 'Mac'].each { platform ->
	tasks.create("test${platform}", Test) {
		useJUnit {
			includeCategories "org.ajoberstar.grgit.fixtures.${platform}Specific"
		}
		System.properties.each { key, value ->
			if (key.startsWith('org.ajoberstar.grgit.test')) {
				systemProperties((key): value)
			}
		}
	}
}

task sourcesJar(type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
	classifier = 'javadoc'
	from javadoc.outputs.files
}

task groovydocJar(type:Jar) {
	classifier = 'groovydoc'
	from groovydoc.outputs.files
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives groovydocJar
}

githubPages {
	repoUri = 'git@github.com:ajoberstar/grgit.git'
	pages {
		from 'src/gh-pages'
		from(javadoc.outputs.files) {
			into 'docs/javadoc'
		}
		from(groovydoc.outputs.files) {
			into 'docs/groovydoc'
		}
	}
}

wrapper {
	gradleVersion = '1.11'
}
